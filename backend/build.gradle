plugins {
    id 'java'
    id 'application'
    id 'io.freefair.lombok' version '9.0.0'
    id 'com.gradleup.shadow' version '9.2.2'
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    mainClass = 'io.restall.sharedex.classifier.api.App'
}

def commonEnv = [
        "CARD_IMAGE_DIR"       : "../card-images",
        "PHASH_BINARY_PATH"    : "outputs/colourPHash.bin",
        "ORB_BIN_PATH"         : "outputs/orbDatabase.bin",
        "UPLOAD_DIR"           : "uploads/",
        "RARITY_MAP_PATH"      : "outputs/rarityMap.json",
        "CARD_LIST_PATH"       : "outputs/cardList.json",
        "RAW_CARD_DETAILS_PATH": "rawCards.json",
        "ALL_CARDS_PATH"       : "outputs/allCards.json",
]

tasks.register('genPHashBin', JavaExec) {
    group = "Execution"
    description = "Generate cache for PHash Comparator"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'io.restall.sharedex.classifier.ColourPHashMatcher'
    environment commonEnv
}

tasks.register('genOrbBin', JavaExec) {
    group = "Execution"
    description = "Generate cache for ORB Comparator"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'io.restall.sharedex.classifier.opencv.PokemonCardRecognizer'
    environment commonEnv
}

tasks.register('processCardsJson', JavaExec) {
    group = "Execution"
    description = "Process cards json file"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'io.restall.sharedex.classifier.collections.CardDetailsProcessing'
    environment commonEnv
}

tasks.register('genLowestRarityMap', JavaExec) {
    group = "Execution"
    description = "Generate map of all card ids to their lowest rarities"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'io.restall.sharedex.classifier.collections.MapLowestRarity'
    environment commonEnv
}

tasks.register('genCardIdList', JavaExec) {
    group = "Execution"
    description = "Generate list of card ids to index for compressing decks"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'io.restall.sharedex.classifier.collections.CardIds'
    environment commonEnv
}

run {
    environment commonEnv
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation 'io.javalin:javalin:6.7.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.0'

    implementation 'com.twelvemonkeys.imageio:imageio-webp:3.12.0'
    implementation 'org.openpnp:opencv:4.9.0-0'

    implementation 'org.apache.commons:commons-math3:3.6.1'
    implementation 'org.slf4j:slf4j-simple:2.0.17'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.14.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.14.0'

    testImplementation 'org.assertj:assertj-core:3.27.6'
}
